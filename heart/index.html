
<!DOCTYPE html>
<html>
<head>
  <title>Heart</title>

  <script src="https://cdnjs.cloudflare.com/ajax/libs/three.js/110/three.min.js" integrity="sha256-gSh8eotzb/CVvCREGPUNgIWuDnTYnZvVOQnRrP1eDjI=" crossorigin="anonymous"></script>
  <script src="https://cdnjs.cloudflare.com/ajax/libs/stats.js/r16/Stats.min.js" integrity="sha256-eQxIuR9Z5Afu5SFVtoND4dHdHygFRhueTk7mwkINjro=" crossorigin="anonymous"></script>
  <style>
    body {
      margin: 0px;
    }
    canvas {
      width: 100%;
      height: 100%;
      display: block;
    }
    p {
      position: absolute;
      right: 0;
      bottom: 0;
      color: rgb(0, 149, 248);
      z-index: 1000;
    }
  </style>
</head>

<body>
  <div id="container"></div>
  <p>Love is mutual.</p>
  <!-- GLSL heart, modified from https://www.shadertoy.com/view/WdK3Dz -->
  <script id="vertexShader" type="x-shader/x-vertex">
    void main() {
      gl_Position = vec4( position, 1.0 );
    }
  </script>
  <script id="fragmentShader" type="x-shader/x-fragment">
    #ifdef GL_ES
    precision mediump float;
    #endif
    uniform vec2 u_resolution;
    uniform float u_time;
    #define POINT_COUNT 15
    vec2 points[POINT_COUNT];
    const float speed = -0.15;
    const float len = 0.24;
    const float scale = 0.012;
    const float intensity = 0.9;
    const float radius = 0.025;
    //https://www.shadertoy.com/view/MlKcDD
    //Signed distance to a quadratic bezier
    float sdBezier(vec2 pos, vec2 A, vec2 B, vec2 C){
      vec2 a = B - A;
      vec2 b = A - 2.0*B + C;
      vec2 c = a * 2.0;
      vec2 d = A - pos;
      float kk = 1.0 / dot(b,b);
      float kx = kk * dot(a,b);
      float ky = kk * (2.0*dot(a,a)+dot(d,b)) / 3.0;
      float kz = kk * dot(d,a);
      float res = 0.0;
      float p = ky - kx*kx;
      float p3 = p*p*p;
      float q = kx*(2.0*kx*kx - 3.0*ky) + kz;
      float h = q*q + 4.0*p3;
      if(h >= 0.0){
        h = sqrt(h);
        vec2 x = (vec2(h, -h) - q) / 2.0;
        vec2 uv = sign(x)*pow(abs(x), vec2(1.0/3.0));
        float t = uv.x + uv.y - kx;
        t = clamp( t, 0.0, 1.0 );
        // 1 root
        vec2 qos = d + (c + b*t)*t;
        res = length(qos);
      }else{
        float z = sqrt(-p);
        float v = acos( q/(p*z*2.0) ) / 3.0;
        float m = cos(v);
        float n = sin(v)*1.732050808;
        vec3 t = vec3(m + m, -n - m, n - m) * z - kx;
        t = clamp( t, 0.0, 1.0 );
        // 3 roots
        vec2 qos = d + (c + b*t.x)*t.x;
        float dis = dot(qos,qos);
        res = dis;
        qos = d + (c + b*t.y)*t.y;
        dis = dot(qos,qos);
        res = min(res,dis);
        qos = d + (c + b*t.z)*t.z;
        dis = dot(qos,qos);
        res = min(res,dis);
        res = sqrt( res );
      }
      return res;
    }
    //http://mathworld.wolfram.com/HeartCurve.html
    vec2 getHeartPosition(float t){
      return vec2(16.0 * sin(t) * sin(t) * sin(t),
      -(13.0 * cos(t) - 5.0 * cos(2.0*t)
      - 2.0 * cos(3.0*t) - cos(4.0*t)));
    }
    //https://www.shadertoy.com/view/3s3GDn
    float getGlow(float dist, float radius, float intensity){
      return pow(radius/dist, intensity);
    }
    float getSegment(float t, vec2 pos, float offset){
      for(int i = 0; i < POINT_COUNT; i++){
        points[i] = getHeartPosition(offset + float(i)*len + fract(speed * t) * 6.2832);
      }
      vec2 c = (points[0] + points[1]) / 2.0;
      vec2 c_prev;
      float dist = 10000.0;
      for(int i = 0; i < POINT_COUNT-1; i++){
        //https://tinyurl.com/y2htbwkm
        c_prev = c;
        c = (points[i] + points[i+1]) / 2.0;
        dist = min(dist, sdBezier(pos, scale * c_prev, scale * points[i], scale * c));
      }
      return max(0.0, dist);
    }
    void main(){
      vec2 pos = vec2(0.5, 0.5) - gl_FragCoord.xy/u_resolution.xy;
      pos.y = pos.y*u_resolution.y/u_resolution.x + 0.03;
      //Get first segment
      float dist = getSegment(u_time, pos, 0.0);
      float glow = getGlow(dist, radius, intensity);
      vec3 col = vec3(0.0);
      //White core
      col += 10.0*vec3(smoothstep(0.006, 0.003, dist));
      //Pink glow
      col += glow * vec3(0.94,0.14,0.4);
      //Get second segment
      dist = getSegment(u_time, pos, 3.1416);
      glow = getGlow(dist, radius, intensity);
      //White core
      col += 10.0*vec3(smoothstep(0.006, 0.003, dist));
      //Blue glow
      col += glow * vec3(0.2,0.6,1.0);
      //Tone mapping
      col = 1.0 - exp(-col);
      //Output to screen
      gl_FragColor = vec4(col,1.0);
    }
  </script>
  <script>
    var container;
    var camera, scene, renderer, stats = createStats();
    var uniforms;
    var clock = new THREE.Clock();
    var heart = new Float32Array(32*2);
    init();
    animate();
    function init() {
      container = document.getElementById( 'container' );
      camera = new THREE.Camera();
      camera.position.z = 1;
      scene = new THREE.Scene();
      var geometry = new THREE.PlaneBufferGeometry( 2, 2 );
      uniforms = {
        u_time: {value: 0.0},
        u_resolution: { value: new THREE.Vector2() },
        u_mouse: { value: new THREE.Vector2() },
        u_heart: { value: new THREE.DataTexture(heart, 32, 1, THREE.RGFormat) }
      };
      var material = new THREE.ShaderMaterial( {
        uniforms: uniforms,
        vertexShader: document.getElementById( 'vertexShader' ).textContent,
        fragmentShader: document.getElementById( 'fragmentShader' ).textContent
      } );
      var mesh = new THREE.Mesh( geometry, material );
      scene.add( mesh );
      renderer = new THREE.WebGLRenderer();
      renderer.setPixelRatio( window.devicePixelRatio );
      container.appendChild( renderer.domElement );
      document.body.appendChild(stats.domElement);
      onWindowResize();
      window.addEventListener( 'resize', onWindowResize, false );
      document.onmousemove = function(e){
        uniforms.u_mouse.value.x = e.pageX
        uniforms.u_mouse.value.y = e.pageY
      }
    }
    function onWindowResize( event ) {
      renderer.setSize( window.innerWidth, window.innerHeight );
      uniforms.u_resolution.value.x = renderer.domElement.width;
      uniforms.u_resolution.value.y = renderer.domElement.height;
    }
    function animate() {
      requestAnimationFrame( animate );
      render();
    }
    function render() {
      uniforms.u_time.value = clock.getElapsedTime();
      renderer.render( scene, camera );
      stats.update();
    }
    function createStats() {
      var stats = new Stats();
      stats.domElement.style.position = 'absolute';
      stats.domElement.style.left = '0';
      stats.domElement.style.bottom = '0';
      return stats;
    }
  </script>

</body>
</html>
